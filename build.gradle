plugins {
    id 'signing'
}

group 'io.github.speedbridgemc'

/// BIG BLOCK O' CODE TO HANDLE MODULAR PROJECT STRUCTURE ///
subprojects {
    apply plugin: 'java' // so components.java is available
    apply plugin: 'maven-publish'

    java {
        withSourcesJar()
        withJavadocJar()
    }

    ext {
        publicationName = ""
        for (String part : name.split("-")) {
            if ("config".equalsIgnoreCase(part))
                continue
            publicationName += part.capitalize()
        }

        isReleaseVersion = false
    }

    afterEvaluate { project ->
        project.ext.isReleaseVersion = !version.endsWith("SNAPSHOT")

        publishing {
            repositories {
                maven {
                    def releaseRepo = 'https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/'
                    def snapshotRepo = 'https://s01.oss.sonatype.org/content/repositories/snapshots/'
                    url = project.ext.isReleaseVersion ? releaseRepo : snapshotRepo
                    credentials {
                        username = rootProject.hasProperty('ossrhUsername') ? ossrhUsername : "Unknown user"
                        password = rootProject.hasProperty('ossrhPassword') ? ossrhPassword : "Unknown password"
                    }
                }
            }

            publications {
                "${project.ext.publicationName}"(MavenPublication) {
                    pom {
                        groupId = rootProject.group
                        artifactId = project.name
                        version = project.version
                        from components.java

                        name = project.name
                        description = project.description
                        url = 'https://speedbridgemc.github.io/config'
                        licenses {
                            license {
                                name = 'MIT License'
                                url = 'http://www.opensource.org/licenses/mit-license.php'
                            }
                        }
                        developers {
                            developer {
                                id = 'ADudeCalledLeo'
                                name = 'Kfir Awad'
                                email = 'kfirawad@gmail.com'
                            }
                        }
                        scm {
                            connection = 'scm:git:git://github.com/SpeedbridgeMC/config.git'
                            developerConnection = 'scm:git:ssh://github.com:SpeedbridgeMC/config.git'
                            url = 'https://github.com/SpeedbridgeMC/config/tree/master'
                        }
                    }
                }
            }

            signing {
                sign publishing.publications[project.ext.publicationName]
            }
            tasks.withType(Sign) {
                onlyIf { project.ext.isReleaseVersion }
            }
        }
    }
}